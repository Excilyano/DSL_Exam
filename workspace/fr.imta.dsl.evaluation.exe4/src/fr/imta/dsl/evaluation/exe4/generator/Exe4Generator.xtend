/*
 * generated by Xtext
 */
package fr.imta.dsl.evaluation.exe4.generator

import fr.imta.dsl.evaluation.exe4.exe4.Button
import fr.imta.dsl.evaluation.exe4.exe4.Frame
import fr.imta.dsl.evaluation.exe4.exe4.Label
import fr.imta.dsl.evaluation.exe4.exe4.Model
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IFileSystemAccess
import org.eclipse.xtext.generator.IGenerator

/**
 * Generates code from your model files on save.
 * 
 * see http://www.eclipse.org/Xtext/documentation.html#TutorialCodeGeneration
 */
class Exe4Generator implements IGenerator {
	
	override void doGenerate(Resource resource, IFileSystemAccess fsa) {
		for (m : resource.allContents.toIterable.filter(Model)) {
			fsa.generateFile(
				"FrameApplication.java",
	        	m.generateProgram)
		}
	}
	
	def generateProgram(Model m) '''
		import javax.swing.JFrame;
		import javax.swing.JLabel;
		import javax.swing.JButton;
		import javax.swing.SwingUtilities;
		import java.awt.FlowLayout;
		import java.awt.event.ActionEvent;
		import java.awt.event.ActionListener;
		
		public class FrameApplication {
			public static void main(String[] args) {
				SwingUtilities.invokeLater(new Runnable() {
					public void run() {
						«FOR f : m.frames»
							final JFrame «f.name» = new JFrame(); // Annotation "final" necessaire sur mon poste
						«ENDFOR»
						«FOR f : m.frames»
							«f.generate»
						«ENDFOR»
						«m.frames.get(0).name».setVisible(true);
					}
				});
			}
		}
	'''
	
	def generate(Frame f) '''
		«f.name».setLayout(new FlowLayout());
		«f.name».setDefaultCloseOperation(JFrame.EXIT_ON_CLOSE);
		«f.name».setTitle("Frame «f.name»");
		«f.name».setSize(«f.width.value», «f.height.value»);
		«FOR c : f.content.components»
            «c.generateComponent(f.name, f.content.components.indexOf(c))»
        «ENDFOR»
	'''
	
	def dispatch generateComponent(Label l, String fName, int i) '''
		JLabel label«fName»«i» = new JLabel();
		«fName».add(label«fName»«i»);
		label«fName»«i».setText("«l.value»");
	'''
	
	def dispatch generateComponent(Button b, String fName, int i) '''
		JButton button«fName»«i» = new JButton();
		«fName».add(button«fName»«i»);
		button«fName»«i».setText("«b.value»");
		button«fName»«i».addActionListener(new ActionListener() {
			//@Override // --> Fonctionne sans l'annotation sur mon poste, decommenter sinon
			public void actionPerformed(ActionEvent e)
			{
				«fName».setVisible(false);
				«b.dest.name».setVisible(true);
			}
		});
	'''
}
